# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
_version=web

# Module Versions
_about_url="https://github.com/fusion809/about"
_about_arch_ver=1.5.16
_atom_typescript_ver=8.10.2
_dark_bint_syntax_ver=0.8.4
_electron_ver=0.36.12
_file_icons_ver=1.7.13
_fusion_ui_ver=0.10.3
_git_plus_ver=5.14.0
_git_time_machine_ver=1.5.3
_language_gfm2_ver=0.90.3
_language_ini2_ver=1.17.2
_language_liquid_ver=0.5.1
_language_lua_ver=0.9.4
_language_shellscript_ver=0.22.3
_markdown_scroll_sync_ver=2.1.2
_markdown_toc_ver=0.4.1
_minimap_ver=4.24.3
_package_sync_ver=1.1.0
_pigments_ver=0.28.4
_script_ver=3.7.3
_platformio_ide_terminal_ver=2.0.10

pkgname=${_pkgname}-editor-${_version}
pkgver=1.7.4
pkgrel=1
pkgdesc='Hackable text editor for the 21st Century, based on web technologies with several additional web development packages installed. Perfect for bloggers, web admins and web developers.'
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-bin' 'atom-editor')
install=${_pkgname}-${_version}.install
source=("${_pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz"
"about-arch-${_about_arch_ver}.tar.gz::$_about_url/archive/v${_about_arch_ver}.tar.gz"
"${_pkgname}.desktop"
"${_pkgname}"
#"git+https://github.com/skandasoft/browser-plus.git"
)
sha256sums=('6ef3bb4c2fea85a7d1c7665fef8aac4e353c8f8da5e2878fea3aecf5437383d4'
            'de170455e00c684c452acdda5dbfa1182370cd2025990f944bfa48e0df685c6d'
            'fb6527d029c5edffa89a5085cc1d3eaaa892e18cc2aefa8ce855be7612da4788'
            '0b3e189202c82c98bc3d3f9a9c88d2aab88537cdd2e0fc2f4d537f55a068acb5')

prepare() {
  cd "$srcdir/${_pkgname}-$pkgver"

  ## package.json edits
  sed -i -e "/exception-reporting/d" \
         -e "/metrics/d" package.json

  sed -i -e "s/\"about\": \".*\"/\"about-arch\": \"${_about_arch_ver}\"/g" package.json # switching about with about-arch

  sed -i -e "s/\"electronVersion\": \".*\"/\"electronVersion\": \"${_electron_ver}\"/g" package.json              # change electron ver

  sed -i -e "/\"find-and-replace\": \".*\",/i \
              \"file-icons\": \"${_file_icons_ver}\"," package.json # file-icons

  sed -i -e "/\"git-diff\": \".*\",/a \
              \"git-plus\": \"${_git_plus_ver}\",\n    \"git-time-machine\": \"${_git_time_machine_ver}\"," package.json # git packages

  sed -i -e "s/\"language-gfm\": \".*\",/\"language-gfm2\": \"${_language_gfm2_ver}\",/g" package.json # GFM2

  sed -i -e "/\"language-hyperlink\": \".*\",/a \
              \"language-ini2\": \"${_language_ini2_ver}\"," package.json # INI2

  sed -i -e "/\"language-less\": \".*\",/a \
              \"language-liquid\": \"${_language_liquid_ver}\",\n    \"language-lua\": \"${_language_lua_ver}\"," package.json # Add Liquid & Lua

  sed -i -e "s/\"language-shellscript\": \".*\"/\"language-shellscript\": \"${_language_shellscript_ver}\"/g" package.json

  sed -i -e "/\"package-generator\": \".*\",/a \
              \"package-sync\": \"${_package_sync_ver}\",\n    \"pigments\": \"${_pigments_ver}\",\n    \"script\": \"${_script_ver}\",\n    \"platformio-ide-terminal\": \"${_platformio_ide_terminal_ver}\"," package.json

  sed -i -e "/\"markdown-preview\": \".*\",/a \
              \"markdown-scroll-sync\": \"${_markdown_scroll_sync_ver}\",\n    \"markdown-toc\": \"${_markdown_toc_ver}\",\n    \"minimap\": \"${_minimap_ver}\"," package.json

##  sed -i -e "/\"bracket-matcher\": \".*\",/a \
  #            \"browser-plus\": \"0.0.60\"," package.json

  sed -i -e "s|^\"|    \"|g"  package.json # fixing spacing issues

  chmod 755 -R package.json

  sed -i -e 's@node script/bootstrap@node script/bootstrap --no-quiet@g' \
  ./script/build || die "Fail fixing verbosity of script/build"

  if ! [[ -d node_modules ]]; then
    mkdir -p node_modules
  else
    rm -rf node_modules/about-arch
  fi

  mv $srcdir/about-${_about_arch_ver} $srcdir/about-arch
  mv $srcdir/about-arch node_modules
  sed -i -e "s/atom-editor/atom-editor-${_version}/g" node_modules/about-arch/lib/about-view.coffee

  sed -i -e "s/<%=Desc=%>/$pkgdesc/g" ${srcdir}/${_pkgname}.desktop
}

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  export PYTHON=/usr/bin/python2
  until ./script/build --build-dir "$srcdir/${_pkgname}-build"; do :; done
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  script/grunt install --build-dir "$srcdir/${_pkgname}-build" --install-dir "$pkgdir/usr"

  install -Dm755 "$srcdir/${_pkgname}" "$pkgdir/usr/bin/${_pkgname}"

  rm "$pkgdir/usr/share/applications/${_pkgname}.desktop"

  install -Dm644 "$srcdir/${_pkgname}.desktop" "$pkgdir/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "resources/app-icons/stable/png/1024.png" "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
  install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
