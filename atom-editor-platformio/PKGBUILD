# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
_version=platformio

# Module Versions
_about_url="https://github.com/fusion809/about"
_about_arch_ver=1.5.15
_electron_ver=0.36.12
_platformio_ide_ver=1.2.2
_platformio_ide_terminal_ver=2.0.10

pkgname=${_pkgname}-editor-${_version}
pkgver=1.7.4
pkgrel=1
pkgdesc="PlatformIO IDE, built on GitHub's Atom text editor."
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-bin')
install=${_pkgname}-${_version}.install
source=("${_pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz"
"about-arch-${_about_arch_ver}.tar.gz::$_about_url/archive/v${_about_arch_ver}.tar.gz"
"${_pkgname}-${_version}.desktop"
"${_pkgname}-${_version}")
sha256sums=('6ef3bb4c2fea85a7d1c7665fef8aac4e353c8f8da5e2878fea3aecf5437383d4'
            'de170455e00c684c452acdda5dbfa1182370cd2025990f944bfa48e0df685c6d'
            '2202363f2b9b48fd20de8442b9c8d12162230e2a3b3c8b4bcf54790c0d476cc9'
            'b5d8abe7b068ea6a6cd2d4eaffdb608cd951fda85e08e4460f00bdb48715040c')

prepare() {
  cd "$srcdir/${_pkgname}-$pkgver"

  ## package.json edits
  sed -i -e "/exception-reporting/d" \
         -e "/metrics/d" package.json  # Remove packages with privacy/security concerns

  sed -i -e "/\"notifications\": \".*\",/a \
              \"platformio-ide\": \"${_platformio_ide_ver}\",\n    \"platformio-ide-terminal\": \"${_platformio_ide_terminal_ver}\"," package.json # install nuclide

  sed -i -e "s/\"language-gfm\": \".*\",/\"language-gfm2\": \"${_language_gfm2_ver}\",/g" package.json # GFM2

  sed -i -e "s/\"about\": \".*\"/\"about-arch\": \"${_about_arch_ver}\"/g" \
         -e "s/\"electronVersion\": \".*\"/\"electronVersion\": \"${_electron_ver}\"/g" \
         -e "s|^\"|    \"|g"  package.json # repl. about with about-arch, adjust electron ver (freetype2) and fixing spacing issues

  chmod 755 -R package.json

  sed -i -e 's@node script/bootstrap@node script/bootstrap --no-quiet@g' \
  ./script/build || die "Fail fixing verbosity of script/build"

  if ! [[ -d node_modules ]]; then
    mkdir -p node_modules
  else
    rm -rf node_modules/about-arch
  fi

  mv $srcdir/about-${_about_arch_ver} $srcdir/about-arch
  mv $srcdir/about-arch node_modules
  sed -i -e "s/atom-editor/atom-editor-${_version}/g" node_modules/about-arch/lib/about-view.coffee

  sed -i -e "s/<%=Desc=%>/$pkgdesc/g" ${srcdir}/${_pkgname}-${_version}.desktop
}

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  export PYTHON=/usr/bin/python2
  until ./script/build --build-dir "$srcdir/${_pkgname}-build"; do :; done
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  script/grunt install --build-dir "$srcdir/${_pkgname}-build" --install-dir "$pkgdir/usr"

  mv "$pkgdir/usr/share/${_pkgname}" "$pkgdir/usr/share/${_pkgname}-${_version}"

	rm "$pkgdir/usr/bin/${_pkgname}"

	rm "$pkgdir/usr/bin/apm"

  install -Dm755 "$srcdir/${_pkgname}-${_version}" "$pkgdir/usr/bin/${_pkgname}-${_version}"

	ln -sf "${pkgdir}/usr/share/${_pkgname}-${_version}/resources/app/apm/node_modules/.bin/apm" "${pkgdir}/usr/bin/apm-${_version}"

  rm "$pkgdir/usr/share/applications/${_pkgname}.desktop"

  install -Dm644 "$srcdir/${_pkgname}-${_version}.desktop" "$pkgdir/usr/share/applications/${_pkgname}-${_version}.desktop"
  install -Dm644 "resources/app-icons/stable/png/1024.png" "$pkgdir/usr/share/pixmaps/${_pkgname}-${_version}.png"
  install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
