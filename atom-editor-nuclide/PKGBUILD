# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
_version=nuclide

# Module Versions
_about_url="https://github.com/fusion809/about"
_about_arch_ver=1.5.16
_electron_ver=0.36.12
_language_gfm2_ver=0.90.3
_language_liquid_ver=0.5.1
_nuclide_ver=0.140.0

pkgname=${_pkgname}-editor-${_version}
pkgver=1.7.4
pkgrel=1
pkgdesc="Facebook's Nuclide IDE, built on GitHub's Atom text editor."
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-bin' 'atom-editor')
install=${_pkgname}-${_version}.install
source=("${_pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz"
"about-arch-${_about_arch_ver}.tar.gz::$_about_url/archive/v${_about_arch_ver}.tar.gz"
"${_pkgname}-${_version}.desktop"
"${_pkgname}-${_version}")
sha256sums=('6ef3bb4c2fea85a7d1c7665fef8aac4e353c8f8da5e2878fea3aecf5437383d4'
            '1f9d3d560ab6374b38644e9c55a7c8fa0f8f581758fb2ee0b18c3f8c53aee44f'
            '2ef6625bb3cf0a7192b183854603b133e24392d14ed0b4340c43feb9693b34c4'
            '0b3e189202c82c98bc3d3f9a9c88d2aab88537cdd2e0fc2f4d537f55a068acb5')

prepare() {
  cd "$srcdir/${_pkgname}-$pkgver"

  ## package.json edits
  sed -i -e "/exception-reporting/d" \
         -e "/metrics/d" package.json  # Remove packages with privacy/security concerns

  sed -i -e "s/\"language-gfm\": \".*\",/\"language-gfm2\": \"${_language_gfm2_ver}\",\n    \"language-liquid\": \"${_language_liquid_ver}\",/g" package.json # GFM2

  sed -i -e "/\"notifications\": \".*\",/a \
              \"nuclide\": \"${_nuclide_ver}\"," package.json # install nuclide

  sed -i -e "s/\"about\": \".*\"/\"about-arch\": \"${_about_arch_ver}\"/g" \
         -e "s/\"electronVersion\": \".*\"/\"electronVersion\": \"${_electron_ver}\"/g" \
         -e "s|^\"|    \"|g"  package.json # repl. about with about-arch, adjust electron ver (freetype2) and fixing spacing issues

  chmod 755 -R package.json

  sed -i -e 's@node script/bootstrap@node script/bootstrap --no-quiet@g' \
  ./script/build || die "Fail fixing verbosity of script/build"

  if ! [[ -d node_modules ]]; then
    mkdir -p node_modules
  else
    rm -rf node_modules/about-arch
  fi

  mv $srcdir/about-${_about_arch_ver} $srcdir/about-arch
  mv $srcdir/about-arch node_modules
  sed -i -e "s/atom-editor/atom-editor-${_version}/g" node_modules/about-arch/lib/about-view.coffee

  sed -i -e "s/<%=Desc=%>/$pkgdesc/g" ${srcdir}/${_pkgname}-${_version}.desktop
}

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  export PYTHON=/usr/bin/python2
  until ./script/build --build-dir "$srcdir/${_pkgname}-build"; do :; done
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  script/grunt install --build-dir "$srcdir/${_pkgname}-build" --install-dir "$pkgdir/usr"

  install -Dm755 "$srcdir/${_pkgname}-${_version}" "$pkgdir/usr/bin/${_pkgname}"

  install -Dm644 "$srcdir/${_pkgname}-${_version}.desktop" "$pkgdir/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "$srcdir/${_version}.png" "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
  install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/atom-LICENSE.md"
  install -Dm644 "node_modules/nuclide/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/nuclide-LICENSE.md"
}
