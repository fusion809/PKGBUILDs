# Maintainer: Michael Hansen <zrax0111 gmail com>

pkgname=visual-studio-code-git
_pkgname=${pkgname//visual-studio-/}
pkgdesc='Visual Studio Code for Linux, Open Source version from git'
pkgver=1.2.0
pkgrel=1
arch=('i686' 'x86_64')
url='https://code.visualstudio.com/'
license=('MIT')
makedepends=('npm' 'gulp' 'python2')
depends=('gtk2' 'gconf' 'alsa-lib' 'libnotify' 'libxtst' 'nodejs' 'nss')
provides=('visual-studio-code-git')

source=("git+https://github.com/Microsoft/vscode"
        "${pkgname}.desktop"
        'product.json'
        "code.png")
sha1sums=('SKIP'
          '9d934cde0c90f52026f4353d94db598b827ff180'
          '534ced051ff7b90c3f7bd01396c55e74055d2516'
          '6273c7a03221f2dea7f92604b483da4d70b2a62b')

case "$CARCH" in
    i686)
        _vscode_arch=ia32
        ;;
    x86_64)
        _vscode_arch=x64
        ;;
    *)
        # Needed for mksrcinfo
        _vscode_arch=DUMMY
        ;;
esac

pkgver() {
    cd "${srcdir}/vscode"
    git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/vscode"

    local _commit=$(cd "${srcdir}/vscode" && git rev-parse HEAD)
    cp $srcdir/product.json .
    sed "s/@commit@/${_commit}/g" -i product.json
}

build() {
    cd "${srcdir}/vscode"

    ./scripts/npm.sh install
    node --max_old_space_size=2000 /usr/bin/gulp vscode-linux-${_vscode_arch}
}

package() {
    install -m 0755 -d "${pkgdir}/opt/VSCode-git"
    cp -r "${srcdir}/VSCode-linux-${_vscode_arch}"/* "${pkgdir}/opt/VSCode-git"

    # Get the better icon
    install -Dm644 "$srcdir/code.png" "$pkgdir/opt/VSCode-git/resources/app/resources/linux/code.png"

    # Include symlink in system bin directory
    install -m 0755 -d "${pkgdir}/usr/bin"
    ln -s '/opt/VSCode-git/code-oss' "${pkgdir}/usr/bin/${_pkgname}"

    # Add .desktop file
    install -D -m644 "${srcdir}/${pkgname}.desktop" \
            "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

    # Install license file
    install -D -m644 "${srcdir}/VSCode-linux-${_vscode_arch}/resources/app/LICENSE.txt" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
