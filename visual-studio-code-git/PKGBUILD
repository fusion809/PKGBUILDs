# Maintainer: Michael Hansen <zrax0111 gmail com>

pkgname=visual-studio-code-git
pkgdesc='Visual Studio Code for Linux, Open Source version from git'
pkgver=1.1.0.insider.r439.g868570c
pkgrel=1
arch=('i686' 'x86_64')
url='https://code.visualstudio.com/'
license=('MIT')
makedepends=('npm' 'gulp' 'python2')
depends=('gtk2' 'gconf' 'alsa-lib' 'libnotify' 'libxtst' 'nodejs' 'nss')
provides=('visual-studio-code-git')

source=("git+https://github.com/Microsoft/vscode"
        "${pkgname}.desktop"
        'product.json'
        "code.png"
        "ini-package.json"
        "shell-package.json")
sha1sums=('SKIP'
          'de838a1c4e6aa37434792c908e042aab90ac421d'
          '534ced051ff7b90c3f7bd01396c55e74055d2516'
          '6273c7a03221f2dea7f92604b483da4d70b2a62b'
          '4631d288314af80d0061152e6b5773752341156b'
          '9b97ec7d45952e72bdda51e51005db596d0b74c8')

case "$CARCH" in
    i686)
        _vscode_arch=ia32
        ;;
    x86_64)
        _vscode_arch=x64
        ;;
    *)
        # Needed for mksrcinfo
        _vscode_arch=DUMMY
        ;;
esac

pkgver() {
    cd "${srcdir}/vscode"
    git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/vscode"

    local _commit=$(cd "${srcdir}/vscode" && git rev-parse HEAD)
    cp $srcdir/product.json .
    sed "s/@commit@/${_commit}/g" -i product.json
    cp $srcdir/ini-package.json extensions/ini/package.json
    cp $srcdir/shell-package.json extensions/shellscript/package.json
}

build() {
    cd "${srcdir}/vscode"

    ./scripts/npm.sh install
    node --max_old_space_size=2000 /usr/bin/gulp vscode-linux-${_vscode_arch}
}

package() {
    install -m 0755 -d "${pkgdir}/opt/VSCode-git"
    cp -r "${srcdir}/VSCode-linux-${_vscode_arch}"/* "${pkgdir}/opt/VSCode-git"

    # Get the better icon
    install -Dm644 "$srcdir/code.png" "$pkgdir/opt/VSCode-git/resources/app/resources/linux/code.png"

    # Include symlink in system bin directory
    install -m 0755 -d "${pkgdir}/usr/bin"
    ln -s '/opt/VSCode-git/code-oss' "${pkgdir}/usr/bin/${pkgname}"

    # Add .desktop file
    install -D -m644 "${srcdir}/${pkgname}.desktop" \
            "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Install license file
    install -D -m644 "${srcdir}/VSCode-linux-${_vscode_arch}/resources/app/LICENSE.txt" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
