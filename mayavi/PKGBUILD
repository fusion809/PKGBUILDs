# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

# Some edits by Bonlin from 4.4.2 to get 4.4.4 working.

pkgname=mayavi
pkgver=4.4.4
pkgrel=1
pkgdesc="A 3-dimensional visualizer of scientific data"
arch=('i686' 'x86_64')
url="https://github.com/enthought/mayavi"
license=('BSD')
depends=('ipython2' 'vtk6' 'python2-envisage' 'python2-pyqt4')
makedepends=('python2-setuptools' 'python2-sphinx' 'python2-twisted' 'xorg-server-xvfb')
replaces=('python2-mayavi')
provides=('python2-mayavi')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/mayavi/archive/${pkgver}.tar.gz")
md5sums=('31a95371bdec9affd97b178b311afb9c')


prepare() {

  export PYTHONPATH=/opt/vtk6/lib/python2.7/site-packages/:$PYTHONPATH
  export LD_LIBRARY_PATH=/opt/vtk6/lib/:$LD_LIBRARY_PATH

  echo "export ETS_TOOLKIT=\"wx\"" > mayavi.sh
  echo "setenv ETS_TOOLKIT \"wx\"" > mayavi.csh

  cd "$srcdir"/mayavi-$pkgver

  # force selection of wxpython 3.0
  sed -e "s/wxversion.ensureMinimal('2.8')/wxversion.select('3.0')/g" -i $(find . -name '*.py')

  # Use python2-sphinx provided sphinx-build2
  sed -i -e 's/sphinx-build/sphinx-build2/' docs/Makefile*

  # set path so autodoc can find just built modules
  echo "sys.path.append('$srcdir/enthought-mayavi-${_githubtag}/build/lib.linux-$CARCH-2.7/')" >> docs/source/mayavi/conf.py

  # fix python->python2
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*[\._]py')
}

build() {

  export PYTHONPATH=/opt/vtk6/lib/python2.7/site-packages/:$PYTHONPATH
  export LD_LIBRARY_PATH=/opt/vtk6/lib/:$LD_LIBRARY_PATH

  cd "$srcdir"/mayavi-$pkgver
  rm -rf build
  mkdir build
  mkdir -p build/lib.linux-$CARCH-2.7/{tvtk/plugins/scene,mayavi/preferences,mayavi/core}
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/tvtk_classes.zip \
    build/lib.linux-$CARCH-2.7/tvtk/tvtk_classes.zip
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/mayavi/preferences/preferences.ini \
    build/lib.linux-$CARCH-2.7/mayavi/preferences/preferences.ini
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/plugins/scene/preferences.ini \
    build/lib.linux-$CARCH-2.7/tvtk/plugins/scene/preferences.ini

  # force using Qt during build
  # export ETS_TOOLKIT=qt4

  # now this is ugly, but docs do not build without X. We setup X according to:
  # http://docs.enthought.com/mayavi/mayavi/tips.html#rendering-using-the-virtual-framebuffer
  export DISPLAY=:69
  Xvfb $DISPLAY &

  # build!
  python2 setup.py build

  # have to kill the server to proceed, and do other cleanup
  kill `cat /tmp/.X${DISPLAY/:/}-lock`
  rm build/lib.linux-$CARCH-2.7/tvtk/tvtk_classes.zip
  rm build/lib.linux-$CARCH-2.7/mayavi/preferences/preferences.ini
  rm build/lib.linux-$CARCH-2.7/tvtk/plugins/scene/preferences.ini
}

package() {

  export PYTHONPATH=/opt/vtk6/lib/python2.7/site-packages/:$PYTHONPATH
  export LD_LIBRARY_PATH=/opt/vtk6/lib/:$LD_LIBRARY_PATH

  cd "$srcdir"/mayavi-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  cp -r docs/build/* "${pkgdir}"/usr/lib/python2.7/site-packages

  # make sure wx is on by default
  install -d "${pkgdir}"/etc/profile.d
  install -Dm644 "${srcdir}"/mayavi.sh "${pkgdir}"/etc/profile.d/mayavi.sh
  install -Dm644 "${srcdir}"/mayavi.csh "${pkgdir}"/etc/profile.d/mayavi.csh

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
