# Contributor: Matheus de Alcantara <matheus.de.alcantara@gmail.com>
# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
_version=beta
pkgname=${_pkgname}-editor-${_version}
_pkgrel=4
_pkgver=1.8.0
pkgver="${_pkgver}.${_pkgrel}"
pkgrel=2
pkgdesc='Chrome-based text editor from GitHub - Beta Channel'
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
depends=('alsa-lib' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-beta-bin')
install=atom.install
source=("$url/archive/v${_pkgver}-${_version}${_pkgrel}.tar.gz")
sha256sums=('2983fe7e062a9e8c9d8816ed9030ca17584402b5a90defa1ecb3ff26141e6922')

prepare() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

	sed -i -e "/exception-reporting/d" \
	       -e "/metrics/d" \
				 -e "s/0.36.8/0.36.12/g" \
				 -e 's/"language-gfm": "0.85.0",/"language-gfm2": "0.88.2",\n    "language-liquid": "0.5.1",/g' package.json

	chmod 755 -R package.json

  sed -e "s/<%= description %>/$pkgdesc/" \
      -e "s|<%= appName %>|Atom Beta|"\
      -e "s|<%= installDir %>/share/<%= appFileName %>/${_pkgname}|/usr/bin/${_pkgname}-${_version}|"\
      -e "s|<%= iconPath %>|${_pkgname}-${_version}.png|"\
      -e "s|GNOME;||" \
      -e "s|text/plain;|application/javascript;application/json;application/postscript;application/x-desktop;application/x-httpd-eruby;application/x-httpd-php;application/x-httpd-php3;application/x-httpd-php4;application/x-httpd-php5;application/x-ruby;application/x-bash;application/x-csh;application/x-sh;application/x-zsh;application/x-shellscript;application/x-sql;application/x-tcl;application/xhtml+xml;application/xml;application/xml-dtd;application/xslt+xml;text/coffeescript;text/css;text/html;text/plain;text/xml;text/xml-dtd;text/x-bash;text/x-c++;text/x-c++hdr;text/x-c++src;text/x-c;text/x-chdr;text/x-csh;text/x-csrc;text/x-dsrc;text/x-diff;text/x-fortran;text/x-go;text/x-java;text/x-java-source;text/x-lua;text/x-makefile;text/x-markdown;text/x-objc;text/x-pascal;text/x-perl;text/x-php;text/x-python;text/x-ruby;text/x-sh;text/x-zsh;text/yaml;inode/directory;|" \
      resources/linux/${_pkgname}.desktop.in > resources/linux/${_pkgname}-${_version}.desktop
}

build() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

	export PYTHON=/usr/bin/python2
	until ./script/build --build-dir "$srcdir/${_pkgname}-build"; do :; done
}

package() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

	script/grunt install --build-dir "$srcdir/${_pkgname}-build" --install-dir "$pkgdir/usr"

	if [[ -d "$pkgdir/usr/share/${_pkgname}" ]]; then
  	mv "$pkgdir/usr/share/${_pkgname}" "$pkgdir/usr/share/${_pkgname}-${_version}"
	fi

  if [[ -f "$pkgdir/usr/bin/${_pkgname}" ]]; then
		mv "$pkgdir/usr/bin/${_pkgname}" "$pkgdir/usr/bin/${_pkgname}-${_version}"
  fi

	if [[ -f "$pkgdir/usr/bin/apm" ]]; then
		rm "$pkgdir/usr/bin/apm"
	fi

	ln -sf "${pkgdir}/usr/share/${_pkgname}-${_version}/resources/app/apm/node_modules/.bin/apm" "${pkgdir}/usr/bin/apm-${_version}"

	if [[ -f "$pkgdir/usr/share/applications/${_pkgname}.desktop" ]]; then
  	rm "$pkgdir/usr/share/applications/${_pkgname}.desktop"
	fi

	install -Dm644 resources/linux/${_pkgname}-${_version}.desktop "$pkgdir/usr/share/applications/${_pkgname}-${_version}.desktop"
	install -Dm644 resources/app-icons/${_version}/png/1024.png "$pkgdir/usr/share/pixmaps/${_pkgname}-${_version}.png"
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
