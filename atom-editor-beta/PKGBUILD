# Contributor: Matheus de Alcantara <matheus.de.alcantara@gmail.com>
# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
_version=beta

# Versions
_about_ver=1.4.2
_about_arch_ver=1.5.7
_language_gfm_ver=0.85.0
_language_gfm2_ver=0.90.2
_language_liquid_ver=0.5.1
_package_generator_ver=1.0.0
_package_sync_ver=1.1.0

pkgname=${_pkgname}-editor-${_version}
_pkgrel=4
_pkgver=1.8.0
pkgver="${_pkgver}.${_pkgrel}"
pkgrel=3
pkgdesc='Hackable text editor for the 21st Century, built using web technologies.'
arch=('x86_64' 'i686')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
depends=('alsa-lib' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-beta-bin')
install=atom.install
source=("$url/archive/v${_pkgver}-${_version}${_pkgrel}.tar.gz"
"${_pkgname}-${_version}.desktop")
sha256sums=('2983fe7e062a9e8c9d8816ed9030ca17584402b5a90defa1ecb3ff26141e6922'
            'a131c08d6a41874d750ca3ae987551c994f6b543b687c2ec8f76195d6b8c952a')

prepare() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

  sed -i -e "/exception-reporting/d" \
	       -e "/metrics/d" \
         -e 's/0.36.8/0.36.12/g' \
         -e "s/\"language-gfm\": \"${_language_gfm_ver}\",/\"language-gfm2\": \"${_language_gfm2_ver}\",\n    \"language-liquid\": \"${_language_liquid_ver}\",/g" \
         -e "s/\"about\": \"${_about_ver}\"/\"about-arch\": \"${_about_arch_ver}\"/g" \
         package.json

	chmod 755 -R package.json

	sed -i -e "s/<%=Desc=%>/$pkgdesc/g" ${srcdir}/${_pkgname}-${_version}.desktop
}

build() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

	export PYTHON=/usr/bin/python2
	until ./script/build --build-dir "$srcdir/${_pkgname}-build"; do :; done
}

package() {
	cd "$srcdir/${_pkgname}-${_pkgver}-${_version}${_pkgrel}"

	script/grunt install --build-dir "$srcdir/${_pkgname}-build" --install-dir "$pkgdir/usr"

	if [[ -d "$pkgdir/usr/share/${_pkgname}" ]]; then
  	mv "$pkgdir/usr/share/${_pkgname}" "$pkgdir/usr/share/${_pkgname}-${_version}"
	fi

  if [[ -f "$pkgdir/usr/bin/${_pkgname}" ]]; then
		mv "$pkgdir/usr/bin/${_pkgname}" "$pkgdir/usr/bin/${_pkgname}-${_version}"
  fi

	if [[ -f "$pkgdir/usr/bin/apm" ]]; then
		rm "$pkgdir/usr/bin/apm"
	fi

	ln -sf "${pkgdir}/usr/share/${_pkgname}-${_version}/resources/app/apm/node_modules/.bin/apm" "${pkgdir}/usr/bin/apm-${_version}"

	if [[ -f "$pkgdir/usr/share/applications/${_pkgname}.desktop" ]]; then
  	rm "$pkgdir/usr/share/applications/${_pkgname}.desktop"
	fi

	install -Dm644 $srcdir/${_pkgname}-${_version}.desktop "$pkgdir/usr/share/applications/${_pkgname}-${_version}.desktop"
	install -Dm644 resources/app-icons/${_version}/png/1024.png "$pkgdir/usr/share/pixmaps/${_pkgname}-${_version}.png"
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
