# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

pkgname=visual-studio-code-oss
pkgdesc='Visual Studio Code for Linux, Open Source version'
_pkgver=1.1.1
pkgver=$(echo $_pkgver | sed 's/-/./g')
pkgrel=1
arch=('i686' 'x86_64')
url='https://code.visualstudio.com/'
license=('MIT')
makedepends=('npm' 'gulp' 'python2')
depends=('gtk2' 'gconf' 'alsa-lib' 'libnotify' 'libxtst' 'nodejs' 'nss')
conflicts=('vscode-oss')
provides=('vscode-oss')

source=("https://github.com/Microsoft/vscode/archive/${_pkgver}.tar.gz"
        "${pkgname}.desktop"
        "product.json"
        "code.png"
        "shell-package.json"
        "ini-package.json")
sha1sums=('71547fefd1eaf174ca7484c668f2186d906cfb67'
          'f6a21d11f0505f256e010da623f9a5a9405e4c91'
          'e03a28a10d97476a8697327ae58891cde611f280'
          '6273c7a03221f2dea7f92604b483da4d70b2a62b'
          '9b97ec7d45952e72bdda51e51005db596d0b74c8'
          '4631d288314af80d0061152e6b5773752341156b')

case "$CARCH" in
    i686)
        _vscode_arch=ia32
        ;;
    x86_64)
        _vscode_arch=x64
        ;;
    *)
        # Needed for mksrcinfo
        _vscode_arch=DUMMY
        ;;
esac

prepare() {
    cd "${srcdir}/vscode-${_pkgver}"

    cp $srcdir/product.json .
    cp $srcdir/ini-package.json extensions/ini/package.json
    cp $srcdir/shell-package.json extensions/shellscript/package.json
}

build() {
    cd "${srcdir}/vscode-${_pkgver}"

    ./scripts/npm.sh install
    node --max_old_space_size=2000 /usr/bin/gulp vscode-linux-${_vscode_arch}
}

package() {
    install -m 0755 -d "${pkgdir}/opt/VSCode-OSS"
    cp -r "${srcdir}/VSCode-linux-${_vscode_arch}"/* "${pkgdir}/opt/VSCode-OSS"

    # Include symlink in system bin directory
    install -m 0755 -d "${pkgdir}/usr/bin"
    ln -s '/opt/VSCode-OSS/code-oss' "${pkgdir}/usr/bin/${pkgname}"

    # Get better icon
    install -Dm644 "$srcdir/code.png" "$pkgdir/opt/VSCode-OSS/resources/app/resources/linux/code.png"

    # Add .desktop file
    install -D -m644 "${srcdir}/${pkgname}.desktop" \
            "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Install license file
    install -D -m644 "${srcdir}/VSCode-linux-${_vscode_arch}/resources/app/LICENSE.txt" \
            "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
