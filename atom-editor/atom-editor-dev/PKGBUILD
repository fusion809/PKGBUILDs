# Maintainer: Brenton Horne <brentonhorne77 at gmail dot com>

_pkgname=atom
pkgname=atom-editor-dev
_gitcommit=aa221a88802abf4df9f1375c52c41d59cf0a12dd
_git=$(echo ${_gitcommit} | cut -c1-7)
pkgver=1.9.0.${_git}
pkgrel=1
pkgdesc='Hackable text editor for the 21st Century, based on web technologies.'
arch=('x86_64' 'i686')
url='https://github.com/atom/atom'
license=('MIT')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk2' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
optdepends=('gvfs: file deletion support')
makedepends=('git' 'npm')
conflicts=('atom-editor-git')
install=atom.install
source=("https://github.com/atom/atom/archive/${_gitcommit}.tar.gz"
"atom-dev")
md5sums=('8ca34b313c55019916aae20d3c2b9083'
         '777e7745acd8188c246541e041e0ce1a')

_gitcommit() {
  git rev-parse HEAD
}

prepare() {
  cd "$srcdir/${_pkgname}-${_gitcommit}"

  sed -i -e "/exception-reporting/d" \
	       -e "/metrics/d" package.json

  #sed -i -e "s/0.37.8/1.0.0/g" package.json

  chmod 755 -R package.json

  sed -e "s/<%= description %>/$pkgdesc/" \
         -e "s/<%= appName %>/Atom Dev/g" \
         -e "s|<%= installDir %>/share/<%= appFileName %>|/usr/bin/atom-dev|g" \
         -e "s/<%= iconPath %>/atom/g" \
         -e "s/GNOME;//g" \
         -e "s|text/plain;|application/javascript;application/json;application/postscript;application/x-csh;application/x-desktop;application/x-httpd-eruby;application/x-httpd-php;application/x-httpd-php3;application/x-httpd-php4;application/x-httpd-php5;application/x-latex;application/x-msdos-program;application/x-ruby;application/x-sh;application/x-shellscript;application/x-sql;application/x-tcl;application/x-tex;application/xhtml+xml;application/xml;application/xml-dtd;application/xslt+xml;text/css;text/csv;text/html;text/plain;text/xml;text/xml-dtd;text/x-asm;text/x-bibtex;text/x-boo;text/x-c++;text/x-c++hdr;text/x-c++src;text/x-c;text/x-chdr;text/x-csh;text/x-csrc;text/x-dsrc;text/x-diff;text/x-eiffel;text/x-fortran;text/x-go;text/x-haskell;text/x-java;text/x-java-source;text/x-lua;text/x-makefile;text/x-markdown;text/x-objc;text/x-pascal;text/x-perl;text/x-php;text/x-python;text/x-ruby;text/x-scala;text/x-scheme;text/x-sh;text/x-tcl;text/x-tex;text/x-vala;text/yaml;inode/directory;|g" \
         resources/linux/atom.desktop.in > resources/linux/atom-dev.desktop
}

build() {
  cd "$srcdir/${_pkgname}-$_gitcommit"
  export PYTHON=/usr/bin/python2
  until ./script/build --build-dir "$srcdir/atom-build"; do :; done
}

package() {
  cd "$srcdir/${_pkgname}-$_gitcommit"

  script/grunt install --build-dir "$srcdir/atom-build" --install-dir "$pkgdir/usr" --channel=dev

  if [[ -d "$pkgdir/usr/share/atom" ]]; then
    mv "$pkgdir/usr/share/atom" "$pkgdir/usr/share/atom-dev"
  fi

  rm "$pkgdir/usr/bin/atom"
  rm "$pkgdir/usr/bin/apm"
  install -Dm644 "$srcdir/atom-dev" "$pkgdir/usr/bin/atom-dev"
  chmod +x "$pkgdir/usr/bin/atom-dev"

  ln -sf "${pkgdir}/usr/share/atom-dev/resources/app/apm/node_modules/.bin/apm" "${pkgdir}/usr/bin/apm-dev"

  if [[ -f "$pkgdir/usr/share/applications/atom.desktop" ]]; then
    rm "$pkgdir/usr/share/applications/atom.desktop"
  fi

  install -Dm644 resources/linux/atom-dev.desktop "$pkgdir/usr/share/applications/atom-dev.desktop"
  install -Dm644 resources/app-icons/dev/png/1024.png "$pkgdir/usr/share/pixmaps/atom-dev.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
